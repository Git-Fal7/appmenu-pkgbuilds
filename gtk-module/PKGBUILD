_pkgbase=vala-panel-appmenu

pkgname=appmenu-gtk-module
_path=subprojects/appmenu-gtk-module
pkgver=0.7.6
pkgrel=1
pkgdesc="Gtk module for exporting menus"

arch=('i686' 'x86_64')
license=('LGPL3')

provides=(unity-gtk-module)

depends=('gtk3' 'gtk2')
makedepends=('meson')

source=("git+https://gitlab.com/vala-panel-project/${_pkgbase}.git"
		80appmenu-gtk-module)
sha256sums=('SKIP'
		'SKIP')

pkgver() {
  cd "${srcdir}/${_pkgbase}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  meson build "${srcdir}/${_pkgbase}/${_path}" --prefix=/usr --libexecdir=lib
  meson compile -C build
}

package()
{
  DESTDIR="$pkgdir" meson install -C build --no-rebuild
  install -Dm755 80appmenu-gtk-module -t "$pkgdir"/etc/X11/xinit/xinitrc.d/
}
