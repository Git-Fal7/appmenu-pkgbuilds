_pkgbase=vala-panel-appmenu

pkgname=${_pkgbase}-mate
pkgver=0.7.6
pkgrel=1
pkgdesc="AppMenu (Global Menu) plugin for mate-panel"

arch=('i686' 'x86_64')
license=('LGPL3')

_opts=(
	--prefix=/usr
	--libdir=lib
	--libexecdir=lib
	-Dauto_features=disabled
	-Dmate=enabled
)

depends=('gtk3' 'bamf>=0.5.0' 'mate-panel' 'libwnck3' 'vala-panel-appmenu-common')
makedepends=('meson' 'vala')
optdepends=('unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar'
            'jayatana: for Java applications support'
            'appmenu-qt4: for qt4 menus')

source=("git+https://gitlab.com/vala-panel-project/${_pkgbase}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgbase}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  meson "${_opts[@]}" build "${srcdir}/${_pkgbase}"
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  msg "Enable mate plugin by writing these"
  msg "gsettings set org.mate.interface gtk-shell-shows-app-menu true"
  msg "gsettings set org.mate.interface gtk-shell-shows-menubar true"
  rm -rf ${pkgdir}/usr/share/{vala-panel-appmenu,locale}
}
