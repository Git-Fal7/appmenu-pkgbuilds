_pkgbase=vala-panel-appmenu

pkgname=${_pkgbase}-xfce
pkgver=0.7.6
pkgrel=1
pkgdesc="AppMenu (Global Menu) plugin for xfce4-panel"

arch=('any')
license=('LGPL3')

_opts=(
	--prefix=/usr
	--libdir=lib
	--libexecdir=lib
	-Dauto_features=disabled
	-Dxfce=enabled
)

depends=('gtk3' 'bamf>=0.5.0' 'xfce4-panel>=4.11.2' 'libwnck3' 'xfconf' 'vala-panel-appmenu-common')
makedepends=('meson' 'vala')
optdepends=('unity-gtk-module: for gtk2/gtk3 menus'
            'vala-panel-appmenu-registrar: for DBusMenu registrar'
            'jayatana: for Java applications support'
            'appmenu-qt4: for qt4 menus')

source=("git+https://gitlab.com/vala-panel-project/${_pkgbase}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${srcdir}/${_pkgbase}"
  ( set -o pipefail
    git describe --long --tags 2>/dev/null | sed 's/\([^-]*-g\)/r\1/;s/-/./g' ||
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
  )
}

build() {
  meson "${_opts[@]}" build "${srcdir}/${_pkgbase}"
  meson compile -C build
}

package() {
  DESTDIR="${pkgdir}" meson install -C build
  msg "Enable xfce plugin by writing these"
  msg "xfconf-query -c xsettings -p /Gtk/ShellShowsMenubar -n -t bool -s true"
  msg "xfconf-query -c xsettings -p /Gtk/ShellShowsAppmenu -n -t bool -s true"
  rm -rf ${pkgdir}/usr/share/{vala-panel-appmenu,locale}
}
